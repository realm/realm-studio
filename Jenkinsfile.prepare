#!groovy

@Library('realm-ci') _

jobWrapper {
  stage('Bump version and tag commit') {
    node('docker') {
      doCheckout()
      // Read the current version of the package
      packageJson = readJSON file: 'package.json'
      currentVersion = packageJson.version
      // Change the package version
      docker.image('node:6.11.2').inside("-e HOME=${env.WORKSPACE} -v /etc/passwd:/etc/passwd:ro") {
        // Bump the version and tag the branch
        // Using --no-git-tag-version because the default does not add the package-lock.json
        nextVersion = sh(
          script: "npm version --no-git-tag-version ${NEXT_VERSION}",
          returnStdout: true,
        ).trim()
        // Update the package-lock.json with this new version - npm version doesn't do that.
        sh("node scripts/copy-version-to-lock.js")
      }
      setBuildName(nextVersion);
      // Push the change to GitHub
      input(message: "Push ${nextVersion} and start a release?", id: 'push')
      // Set the email and name used when committing
      sh 'git config --global user.email "ci@realm.io"'
      sh 'git config --global user.name "Jenkins CI"'
      // Stage the updates to the files, commit and tag the commit
      sh 'git add package.json package-lock.json'
      sh "git commit -m 'Prepare version ${nextVersion}'"
      sh "git tag ${nextVersion}"

      sshagent(['realm-ci-ssh']) {
        // Push with tags
        sh "git push --tags origin HEAD"
      }
      
      // Create a draft release on GitHub
      withCredentials([string(credentialsId: 'github-release-token', variable: 'GITHUB_TOKEN')]) {
        // Read the release body and replace in any variables
        releaseBody = readFile 'RELEASE_BODY.md'
        releaseBody = releaseBody
          .replaceAll("\\[PREVIOUS_VERSION\\]", "v${currentVersion}")
          .replaceAll("\\[CURRENT_VERSION\\]", nextVersion)
        requestBody = JsonOutput.toJson([
          tag_name: nextVersion,
          name: "${nextVersion.substring(1)}: ...",
          body: releaseBody,
          draft: true,
        ])
        // Send a request to GitHub, creating the draft release
        sh """
          curl \
            -H "Content-Type: application/json" \
            -H "Authorization: token ${GITHUB_TOKEN}" \
            -X POST \
            -d '${requestBody}' \
            https://api.github.com/repos/realm/realm-studio/releases
        """
      }
      
      // Start a release job
      build(
        job: 'realm-studio/release',
        parameters: [
          [$class: 'GitParameterValue', name: 'VERSION_TAG', value: nextVersion]
        ],
        propagate: false,
        wait: false
      )
    }
  }
}

def doCheckout() {
  return checkout([
    $class: 'GitSCM',
    branches: [[name: env.BRANCH_NAME]],
    doGenerateSubmoduleConfigurations: false,
    extensions: [
      [$class: 'WipeWorkspace'],
      [$class: 'CleanCheckout'],
      [$class: 'LocalBranch']
    ],
    gitTool: 'native git',
    userRemoteConfigs: [[
      credentialsId: 'realm-ci-ssh',
      name: 'origin',
      refspec: '+refs/tags/*:refs/remotes/origin/tags/* +refs/heads/*:refs/remotes/origin/*',
      url: 'git@github.com:realm/realm-studio.git'
    ]]
  ])
}
